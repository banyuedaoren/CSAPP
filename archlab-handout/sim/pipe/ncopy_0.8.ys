#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Written by fanese-myk, use iaddq, rearrange instruction to avoid load/use hazzard
# use loop unrolling 4*1
# make few changes from ver0.6 to make program run faster(avoid unpredicted branch)
# Line 51 is fundamental
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = l len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
    #xorq %rax,%rax		# count1 = 0;actually we do not need this instruction
	iaddq $-3, %rdx		# calculate len - 3
	jg Loop

RESTT:
	iaddq $3, %rdx
REST:
	jg NOT_FINISHED
	ret
NOT_FINISHED:
	mrmovq (%rdi), %r10		# load *src
	iaddq $8, %rdi
	andq %r10, %r10			# test *src
	jle ADD3				# if not greater than 0, jump
	iaddq $1, %rax			# add the count of postive numbers
ADD3:	
	rmmovq %r10, (%rsi)		# save
	iaddq $8, %rsi
	iaddq $-1, %rdx
	jmp REST
Loop:
	mrmovq (%rdi), %r10	# read val from src...
	iaddq $32, %rdi		# src+=4
	rmmovq %r10, (%rsi) # save *src to *rsi
	andq %r10, %r10		# test %r10
	jle ADD1			# if This number is not greater than 0, than not add %rax
	iaddq $1, %rax		# add the number of postive numbers
ADD1:	
	mrmovq -24(%rdi), %r10	# read the second number
	iaddq $32, %rsi			# dst += 4
	rmmovq %r10, -24(%rsi)	# save *(src + 1) to *(dst + 1)
	andq %r10, %r10			# val <= 0?
	mrmovq -16(%rdi), %r10   # rearrange the instructions to avoid load/use hazzard
	jle ADD2				# if not, add the count
	iaddq $1, %rax			# count++
ADD2:
	rmmovq %r10, -16(%rsi)
	andq %r10, %r10
	mrmovq -8(%rdi), %r10
	jle ADD4
	iaddq $1, %rax
ADD4:
	rmmovq %r10, -8(%rsi)
	andq %r10, %r10
	jle ADD5
	iaddq $1, %rax
ADD5:
	iaddq $-4, %rdx			# len -= 4 if(len - 3 > 0) that means len > 3
	jg Loop					# if so, goto Loop:

	jmp RESTT
##################################################################
# Do not modify the following section of code
# Function epilogue.
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
