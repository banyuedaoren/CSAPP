#/* $begin ncopy-ys */
##################################################################
# ncopy.ys - Copy a src block of len words to dst.
# Return the number of positive words (>0) contained in src.
#
# Include your name and ID here.
# Written by fanese-myk, use iaddq, rearrange instruction to avoid load/use hazzard
# use loop unrolling 3*1
##################################################################
# Do not modify this portion
# Function prologue.
# %rdi = src, %rsi = dst, %rdx = l len
ncopy:

##################################################################
# You can modify this portion
	# Loop header
    #xorq %rax,%rax		# count1 = 0;actually we do not need this instruction
	iaddq $-2, %rdx		# calculate len - 2
	jle RESTT
Loop:
	mrmovq (%rdi), %r10	# read val from src...
	iaddq $24, %rdi		# src+=3
	rmmovq %r10, (%rsi) # save *src to *rsi
	andq %r10, %r10		# test %r10
	jle ADD1			# if This number is not greater than 0, than not add %rax
	iaddq $1, %rax		# add the number of postive numbers
ADD1:	
	mrmovq -16(%rdi), %r10	# read the second number
	iaddq $24, %rsi			# dst += 3
	rmmovq %r10, -16(%rsi)	# save *(src + 1) to *(dst + 1)
	andq %r10, %r10			# val <= 0?
	jle ADD2				# if not, add the count
	iaddq $1, %rax			# count++
ADD2:
	mrmovq -8(%rdi), %r10
	rmmovq %r10, -8(%rsi)
	andq %r10, %r10
	jle ADD4
	iaddq $1, %rax
ADD4:
	iaddq $-3, %rdx			# len -= 3 if(len - 2 > 0) that means len > 2
	jg Loop					# if so, goto Loop:
RESTT:
	iaddq $2, %rdx
REST:
	je Done
	mrmovq (%rdi), %r10		# load *src
	iaddq $8, %rdi
	andq %r10, %r10			# test *src
	jle ADD3				# if not greater than 0, jump
	iaddq $1, %rax			# add the count of postive numbers
ADD3:	
	rmmovq %r10, (%rsi)		# save
	iaddq $8, %rsi
	iaddq $-1, %rdx
	jmp REST
##################################################################
# Do not modify the following section of code
# Function epilogue.
Done:
	ret
##################################################################
# Keep the following label at the end of your function
End:
#/* $end ncopy-ys */
